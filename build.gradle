plugins {
    id 'java'
    id "net.serenity-bdd.serenity-gradle-plugin" version "4.0.30"
}

repositories {
    mavenCentral()
}

ext {
    serenityVersion = '4.0.30'
}

dependencies {
    testImplementation "net.serenity-bdd:serenity-core:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-cucumber:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-junit5:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-reports:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-model:${serenityVersion}"
    
    testImplementation "org.junit.vintage:junit-vintage-engine:5.10.1"
    testImplementation "org.junit.platform:junit-platform-suite:1.10.1"
    testRuntimeOnly "org.junit.platform:junit-platform-suite-engine:1.10.1"
    testRuntimeOnly "io.cucumber:cucumber-junit-platform-engine:7.15.0"
    
    implementation 'org.slf4j:slf4j-simple:2.0.9'
    testImplementation "org.assertj:assertj-core:3.24.2"

    implementation 'com.itextpdf:itext7-core:7.2.5'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.opencsv:opencsv:5.9'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

test {
    useJUnitPlatform()

    jvmArgs(
        "-XX:+EnableDynamicAgentLoading",
        "-Djdk.attach.allowAttachSelf=true",
        "--add-opens", "java.base/java.lang=ALL-UNNAMED",
        "--add-opens", "java.base/java.util=ALL-UNNAMED",
        "--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED"
    )

    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
    ignoreFailures = true
}

task generateEvidence(type: JavaExec) {
    group = "Reporting"
    mainClass = "utils.EvidenceReport"
    classpath = sourceSets.test.runtimeClasspath
    mustRunAfter test, aggregate 
}

// Tarea maestra para correr todo sin complicaciones
task runAllTests {
    dependsOn 'clean', 'test', 'aggregate', 'generateEvidence'
    tasks.findByName('test').mustRunAfter 'clean'
    tasks.findByName('aggregate').mustRunAfter 'test'
    tasks.findByName('generateEvidence').mustRunAfter 'aggregate'
}